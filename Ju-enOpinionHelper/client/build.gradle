plugins {
    id 'java'
    id 'application'
}

group = 'com.koto-thing'
version = '1.0.0'
sourceCompatibility = '21'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.formdev:flatlaf:3.1'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

application {
    mainClass = 'koto_thing.Main'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'koto_thing.Main',
                'Implementation-Version': version,
                'Implementation-Title': 'Ju-en Opinion Helper'
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.register('createInstaller') {
    dependsOn jar

    doLast {
        def jarFile = jar.outputs.files.singleFile
        def installerDir = file("${buildDir}/installer")
        installerDir.mkdirs()

        exec {
            commandLine 'jpackage',
                    '--input', jarFile.parent,
                    '--name', 'JuenOpinionHelper',
                    '--main-jar', jarFile.name,
                    '--main-class', 'koto_thing.Main',
                    '--type', 'msi',
                    '--app-version', version,
                    '--vendor', 'koto-thing',
                    '--dest', installerDir.absolutePath,
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-menu-group', 'Ju-en Opinion Helper',
                    '--java-options', '-Xmx512m',
                    '--runtime-image', System.getProperty('java.home')
        }

        println "インストーラーが作成されました: ${installerDir.absolutePath}"
    }
}


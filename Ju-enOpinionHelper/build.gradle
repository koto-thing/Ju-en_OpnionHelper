plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.koto-thing'
version = '1.0.0'
sourceCompatibility = '21'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.formdev:flatlaf:3.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    archiveFileName = 'JuenOpinionHelper.jar'
    mainClass = 'koto_thing.JuenOpinionHelperApplication'
}

tasks.register('createInstaller') {
    dependsOn bootJar

    doLast {
        def jarFile = file("${buildDir}/libs/JuenOpinionHelper.jar")
        def installerDir = file("${buildDir}/installer")
        installerDir.mkdirs()

        exec {
            commandLine 'jpackage',
                    '--input', jarFile.parent,
                    '--name', 'JuenOpinionHelper',
                    '--main-jar', jarFile.name,
                    '--type', 'msi',
                    '--app-version', version,
                    '--vendor', 'koto-thing',
                    '--dest', installerDir.absolutePath,
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-menu-group', 'Ju-en Opinion Helper',
                    '--java-options', '-Xmx512m',
                    '--runtime-image', System.getProperty('java.home')  // JREを同梱
        }

        println "インストーラーが作成されました: ${installerDir.absolutePath}"
    }
}
